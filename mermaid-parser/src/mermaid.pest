mermaid = {
  SOI ~
  comment_or_newline* ~
  diagram ~
  comment_or_newline* ~
  comment? ~
  EOI
}

WHITESPACE = _{ " " | "\t" }
comment_or_newline = _{ comment? ~ NEWLINE }

diagram = _{ pie_diagram | info }

// pie chart
pie_diagram = _{
  pie_start ~ comment_or_newline? ~
  pie_title? ~ comment_or_newline+ ~
  pie_entry? ~ (comment_or_newline+ ~ pie_entry)*
}
pie_start = { "pie" }
pie_title = { "title" ~ pie_title_value }
pie_title_value = { (!(NEWLINE | comment_start) ~ ANY)* }
pie_entry = { quoted_string ~ ":" ~ number }

// info
info = _{ info_start ~ info_show? }
info_start = { "info" }
info_show = { "showInfo" }

// comment
comment = { comment_start ~ comment_inner }
comment_start = _{ "#" }
comment_inner = { (!NEWLINE ~ ANY)* }

// number
number = @{ ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT+)? }

// json string
quoted_string = ${ "\"" ~ string_inner ~ "\"" }
string_inner = @{ char* }
char = {
  !("\"" | "\\") ~ ANY
  | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
  | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}
